/**  weather_widget.canto
 *
 *   Simple front end component that calls the openweathermap.org
 *   api to get the weather for a zip code.
 *
 *
 **/

site weather_widget {

    /-------- initialization ----------------------/

    init {
    }
    
    session_init {
    }

    /-------- weather model -----------------------/
    
    /** An object corresponding to the object returned by the openweathermap endpoint.
     *
     *  The weather info object does not have to contain every field in the object
     *  returned by the endpoint.  However, the fields that it does contain must
     *  match the corresponding fields in the returned object in name and type.
     **/
    serializable(*) weather_info(str) {

        serializable(*) weather_condition(str) {
            keep: main [/]
            keep: description [/]
        }
        
        serializable(*) main_weather(str) {
            keep: float temp = 0.0
            keep: int pressure = 0
            keep: int humdity = 0
        }
        
        serializable(*) wind_condition(str) {
            keep: float speed = 0.0
            keep: int deg = 0 
        }
        
        keep: weather_condition[] weather = []
        keep: main_weather main [/]
        keep: wind_condition wind [/]
        keep: name [/]
     }


    /-------- weather component -------------------/

    dynamic component weather_component {
    
    }

    SPLASH_LOGO = "images/sponge-logo-512x96.png"

    color main_bgcolor = "#9fdfef"

    /-------- base page ---------------------------/

    page(*) base_page(params{}) {
        boolean ajax_enabled = true
    
        style [| 
            html, body { 
                width: 100%;
                height: 100%;
                margin: 0 0 0 0;
                background-color: {= main_bgcolor; =};
            }

            .centered_container {
                width: 100%;
                height: 100%;
                margin: 0 0 0 0;
                position: relative;
            }
            
            .centered_high_box {
                position: absolute;
                left: 50%;
                top: 38%;
                margin-right: -50%;
                transform: translate(-50%, -50%);
                width: 36em;
            }
            
            .centered_div  {
                width: 100%;
                text-align: center;
             }
            
            .centered_div table {
                margin-left: auto;
                margin-right: auto;
            }

            .centered_div td {
                padding: 0.125em 0.5em;
            }
            
            .centered_div div {
                padding: 0.75em 4em;
            }
            
           .labeled_edit {
                text-align: center;
                font-weight: bold;
                padding:0.5em;
            }
            
            #weather_button {
                width: 10em;
            }
            
        |]        
        
        base_page this_page = owner
        page_name = owner.type
        
        sub;
    }

    /-------- home page ---------------------------/

    public base_page(*) index(params{}) {
        title = "Weather Page"
    
        [| <h1>Weather Info</h1> |]
        
        weather_component;
    }

    /-------- error page --------------------------/
    page error_page(request r) {
        title = "Error"
        color bgcolor = "#AAEEDD"

        error_div(r);        
    }    

    /-------- tests -------------------------------/

    weather_info_sample [|
        {"coord":{"lon":-0.13,"lat":51.51},
         "weather":[{"id":300,"main":"Drizzle","description":"light intensity drizzle","icon":"09d"}],
         "base":"stations",
         "main":{"temp":280.32,"pressure":1012,"humidity":81,"temp_min":279.15,"temp_max":281.15},
         "visibility":10000,
         "wind":{"speed":4.1,"deg":80},
         "clouds":{"all":90},
         "dt":1485789600,
         "sys":{"type":1,"id":5091,"message":0.0103,"country":"GB","sunrise":1485762037,"sunset":1485794875},
         "id":2643743,
         "name":"London",
         "cod":200}
    |]

    test_runner weather_widget_tests {
    
        public test_base deserialize_weather_info {
            expected = "ABCD"
            
            weather_info wi = weather_info(weather_info_sample)
            
            eval(wi);
            
            if (wi.name == "London") {
                "A";
            } else {
                "x";
            }
            
            if (wi.main.temp == 280.32) {
                "B";
            } else {
                "x";
            }
            
            if (wi.weather.count == 1) {
                "C";
            } else {
                "x";
            }
            
            if (wi.weather[0].main == "Drizzle") {
                "D";
            } else {
                "x";
            }
        }
    }

    public page(*) tests(params{}) {
        [| <h2>Test Results</h2> |]

        weather_widget_tests.run;

        [| <ol> |]
        for test_result rslt in weather_widget_tests.results {
            [| <li>{= rslt.name; =}<br> |]
            if (rslt.result) [|
                Passed
            |] else [|
                <span style="color:red">Failed</span>
            |]
            [| <br><ul> |]
            for msg in rslt.messages [|
                <li>{= msg; =}</li>
            |]
            [| </ul></li> |]                
        }
        [| </ol> |]
    }   

}
